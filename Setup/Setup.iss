; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName={cm:ClamWinFreeAntivirus}
AppVerName={cm:ClamWinFreeAntivirus} 0.91.2
OutputBaseFilename=ClamWin-0.91.2-L10N-35
AppPublisher=budtse
AppPublisherURL=http://www.clamwin.com/
AppSupportURL=http://www.clamwin.com/
AppUpdatesURL=http://www.clamwin.com/
DefaultDirName={code:BaseDir}\ClamWin
DefaultGroupName={cm:ClamWinFreeAntivirus}
AllowNoIcons=true
MinVersion=4.1.1998,5.0.2195

ShowLanguageDialog=yes
ShowUndisplayableLanguages=yes
LanguageDetectionMethod=locale
OutputDir=Output
OutputManifestFile=ClamwinL10N-Manifest.txt
Compression=lzma
InternalCompressLevel=max

SolidCompression=false
WizardImageFile=Setupfiles\WizModernImage.bmp
WizardSmallImageFile=Setupfiles\WizModernSmallImage.bmp
[Languages]
Name: ar_AE; MessagesFile: ..\locale\Inno Setup\Arabic.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: cs_CZ; MessagesFile: compiler:Languages\Czech.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: de_DE; MessagesFile: compiler:Languages\German.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: en_US; MessagesFile: compiler:Default.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: es_ES; MessagesFile: compiler:Languages\Spanish.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: fr_FR; MessagesFile: compiler:Languages\French.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: hu_HU; MessagesFile: compiler:Languages\Hungarian.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: it_IT; MessagesFile: compiler:Languages\Italian.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: nl_BE; MessagesFile: compiler:Languages\Dutch.isl; LicenseFile: ..\locale\GPL\License_nl_BE.rtf
Name: pl_PL; MessagesFile: compiler:Languages\Polish.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: pt_BR; MessagesFile: compiler:Languages\BrazilianPortuguese.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: ru_RU; MessagesFile: compiler:Languages\Russian.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf
Name: uk_UA; MessagesFile: ..\locale\Inno Setup\Ukrainian.isl; LicenseFile: ..\locale\GPL\License_en_US.rtf

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: py2exe\dist\bin\python23.dll; DestDir: {app}\bin; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\bin\ClamTray.exe; DestDir: {app}\bin; Components: ClamWin; Flags: restartreplace uninsrestartdelete replacesameversion
Source: py2exe\dist\bin\ClamWin.exe; DestDir: {app}\bin; Components: ClamWin; Flags: restartreplace uninsrestartdelete replacesameversion
Source: py2exe\dist\bin\img\Clam.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\ClamAV.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\netfarm.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\Control.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\Scan.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\ScanMem.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\FrameIcon.ico; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\TrayIcon.ico; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\Title.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\FD-logo.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\PythonPowered.gif; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\World.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\Support.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\ListScan.png; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: py2exe\dist\bin\img\Splash.bmp; DestDir: {app}\bin\img; Components: ClamWin; Flags: ignoreversion
Source: ..\..\..\clamav-release\contrib\msvc\Release\clamscan.exe; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\..\..\clamav-release\contrib\msvc\Release\freshclam.exe; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\..\..\clamav-release\contrib\msvc\Release\sigtool.exe; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\..\..\clamav-release\contrib\msvc\Release\libclamav.dll; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete replacesameversion

; Copy the manuals and Help Files
Source: ..\doc\en_UK\manual_EN.pdf; DestDir: {app}\doc; Components: ClamWin; Flags: ignoreversion
Source: ..\doc\en_UK\manual_en.chm; DestDir: {app}\doc; Components: ClamWin; Flags: ignoreversion; DestName: manual.chm

;Source: ..\doc\pt_BR\Manual_pt.chm; DestDir: {app}\doc; Components: LanguageSupport\Brazilian; Flags: ignoreversion
;Source: ..\doc\pt_BR\Manual_pt.pdf; DestDir: {app}\doc; Components: LanguageSupport\Brazilian; Flags: ignoreversion
Source: ..\doc\nl_BE\Manual_NL.chm; DestDir: {app}\doc; Components: LanguageSupport\Dutch; Flags: ignoreversion
Source: ..\doc\nl_BE\Manual_NL.pdf; DestDir: {app}\doc; Components: LanguageSupport\Dutch; Flags: ignoreversion
Source: ..\doc\fr_FR\manual_fr.pdf; DestDir: {app}\doc; Components: LanguageSupport\French; Flags: ignoreversion
Source: ..\doc\ru_RU\manual_ru.chm; DestDir: {app}\doc; Components: LanguageSupport\Russian; Flags: ignoreversion

; Copy the Gettext locale files
Source: ..\locale\ar_AE.mo; DestDir: {app}\locale\ar_AE\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Arabic; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\de_DE.mo; DestDir: {app}\locale\de_DE\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\German; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\es_ES.mo; DestDir: {app}\locale\es_ES\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Spanish; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\fr_FR.mo; DestDir: {app}\locale\fr_FR\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\French; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\hu_HU.mo; DestDir: {app}\locale\hu_HU\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Hungarian; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\it_IT.mo; DestDir: {app}\locale\it_IT\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Italian; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\nl_BE.mo; DestDir: {app}\locale\nl_BE\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Dutch; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\pl_PL.mo; DestDir: {app}\locale\pl_PL\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Polish; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\pt_BR.mo; DestDir: {app}\locale\pt_BR\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Brasilian; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\ru_RU.mo; DestDir: {app}\locale\ru_RU\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Russian; Flags: restartreplace uninsrestartdelete replacesameversion
Source: ..\locale\uk_UA.mo; DestDir: {app}\locale\uk_UA\LC_MESSAGES; DestName: clamwin.mo; Components: LanguageSupport\Ukrainian; Flags: restartreplace uninsrestartdelete replacesameversion

; on xp and greater VC80 CRT needs to be installed in Microsoft.VC80.CRT
Source: Dependencies\Microsoft.VC80.CRT\Microsoft.VC80.CRT.manifest; DestDir: {app}\bin\Microsoft.VC80.CRT; Components: ClamAV; Check: IsXPOrLater
Source: Dependencies\Microsoft.VC80.CRT\msvcm80.dll; DestDir: {app}\bin\Microsoft.VC80.CRT; Components: ClamAV; Flags: restartreplace uninsrestartdelete; Check: IsXPOrLater
Source: Dependencies\Microsoft.VC80.CRT\msvcr80.dll; DestDir: {app}\bin\Microsoft.VC80.CRT; Components: ClamAV; Flags: restartreplace uninsrestartdelete; Check: IsXPOrLater
Source: Dependencies\Microsoft.VC80.CRT\msvcp80.dll; DestDir: {app}\bin\Microsoft.VC80.CRT; Components: ClamAV; Flags: restartreplace uninsrestartdelete; Check: IsXPOrLater
; on 2000 and 98 VC80 CRT needs to be installed in the same dir; no manifest
Source: Dependencies\Microsoft.VC80.CRT\msvcm80.dll; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete; Check: not IsXPOrLater
Source: Dependencies\Microsoft.VC80.CRT\msvcr80.dll; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete; Check: not IsXPOrLater
Source: Dependencies\Microsoft.VC80.CRT\msvcp80.dll; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete; Check: not IsXPOrLater

;pthread
Source: Dependencies\pthread\pthreadVC2.dll; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete

;Source: ..\clamav-stable\sigtool\sigtool.exe; DestDir: {app}\bin; Components: ClamAV; Flags: restartreplace uninsrestartdelete replacesameversion
; TODO : Copy Unicode build in NT and ANSI in 9x
Source: ..\cpp\Release\ExpShell.dll; DestDir: {app}\bin; Components: ExplorerShell; Flags: restartreplace uninsrestartdelete 32bit; Check: IsWin9x
Source: ..\cpp\Release_Unicode\ExpShell.dll; DestDir: {app}\bin; Components: ExplorerShell; Flags: restartreplace uninsrestartdelete 32bit; Check: UsingWinNT
Source: ..\cpp\Release_x64\ExpShell64.dll; DestDir: {app}\bin; Components: ExplorerShell; Flags: restartreplace uninsrestartdelete 64bit; Check: IsWin64


Source: py2exe\dist\bin\WClose.exe; DestDir: {app}\bin; Components: ClamWin; Flags: restartreplace uninsrestartdelete replacesameversion
Source: py2exe\dist\bin\w9xpopen.exe; DestDir: {app}\bin; Components: ClamWin; Flags: restartreplace uninsrestartdelete replacesameversion
;TODO: Source: Setupfiles\conagent.pif; DestDir: {app}\bin; Components: ClamWin; Flags: 32bit; Check: IsWin9x

Source: py2exe\dist\bin\OlAddin.exe; DestDir: {app}\bin; Components: OutlookAddin; Flags: restartreplace uninsrestartdelete replacesameversion
Source: py2exe\dist\lib\pyclamav.pyd; DestDir: {app}\lib; Components: OutlookAddin; Flags: restartreplace uninsrestartdelete
;Source: {%TEMP|{localappdata}}\clamwin-src.zip; DestDir: {app}\src; Components: Sources; Flags: external ignoreversion
;Source: {%TEMP|{localappdata}}\clamav-src.tar.gz; DestDir: {app}\src; Components: Sources; Flags: external ignoreversion
;Source: {%TEMP|{localappdata}}\cygwin-src.tar.bz2; DestDir: {app}\src; Components: Sources; Flags: external replacesameversion

Source: py2exe\dist\lib\_socket.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\_sre.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\_ssl.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\_bsddb.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\_winreg.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\clamwin.zip; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\datetime.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\mxDateTime.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\exchange.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\exchdapi.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\shell.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\htmlc.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\gizmosc.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\mapi.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\pythoncom23.dll; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\pywintypes23.dll; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\unicodedata.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\bin\w9xpopen.exe; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32api.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32clipboard.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32event.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32file.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32gui.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32pipe.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32process.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32trace.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\win32security.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\wxc.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\wxmsw24uh.dll; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\zlib.pyd; DestDir: {app}\lib; Components: ClamWin; Flags: restartreplace uninsrestartdelete
Source: py2exe\dist\lib\BalloonTip.pyd; DestDir: {app}\lib; Components: ClamWin; Check: IsWin9x; Flags: restartreplace uninsrestartdelete

; added main.cvd as per clamav team request
Source: cvd\main.cvd; DestDir: {code:CommonProfileDir}\.clamwin\db; Components: ClamWin; Flags: ignoreversion comparetimestamp; Check: NoMainInc
Source: cvd\daily.cvd; DestDir: {code:CommonProfileDir}\.clamwin\db; Components: ClamWin; Flags: ignoreversion comparetimestamp; Check: NoDailyInc

[Icons]
Name: {group}\{cm:StartMenuItemVirusScanner}; Filename: {app}\bin\ClamWin.exe; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: ClamWin
Name: {code:DesktopDir}\{cm:ClamWinFreeAntivirus}; Filename: {app}\bin\ClamWin.exe; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: ClamWin; Tasks: desktopicon
Name: {group}\{cm:StartMenuGroupHelp}\{cm:StartMenuItemPrintableManual}; Filename: {app}\doc\manual.pdf; Components: ; WorkingDir: {app}\bin
Name: {group}\{cm:StartMenuGroupHelp}\{cm:StartMenuItemOnlineHelp}; Filename: {app}\doc\manual.chm; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: ClamWin
Name: {group}\{cm:StartMenuGroupHelp}\{cm:StartMenuGroupHelpInternational}\Russian (Russian Help); Filename: {app}\doc\manual_ru.chm; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: LanguageSupport\Russian
Name: {group}\{cm:StartMenuGroupHelp}\{cm:StartMenuGroupHelpInternational}\Français (French Manual); Filename: {app}\doc\manual_fr.pdf; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: LanguageSupport\French
Name: {group}\{cm:StartMenuGroupHelp}\{cm:StartMenuGroupHelpInternational}\Nederlands (Dutch Help); Filename: {app}\doc\Manual_NL.chm; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: LanguageSupport\Dutch
;Name: {group}\{cm:StartMenuGroupHelp}\{cm:StartMenuGroupHelpInternational}\Brazilian (Brazilian Help); Filename: {app}\doc\Manual_pt.chm; WorkingDir: {app}\bin; Comment: {cm:ClamWinFreeAntivirus}; Components: LanguageSupport\Brazilian
Name: {group}\Uninstall ClamWin Free Antivirus; Filename: {uninstallexe}
[Run]
; NOTE: The following entry contains an English phrase ("Launch"). You are free to translate it into another language if required.
Filename: {app}\bin\ClamWin.exe; Parameters: --mode=update --close; WorkingDir: {app}\bin; StatusMsg: {cm:DownloadDB2}; Components: ClamWin; Tasks: DownloadDB
Filename: {app}\bin\ClamTray.exe; WorkingDir: {app}\bin; Flags: nowait; Components: ClamWin
[Dirs]
; create clamwin folders in common profiles dir if all users is selected and set permissions so that other users can modify
Name: {code:CommonProfileDir}\.clamwin\db; Components: ClamAV; Permissions: authusers-full; Check: IsAllUsers
Name: {code:CommonProfileDir}\.clamwin\log; Components: ClamAV; Permissions: authusers-full; Check: IsAllUsers
Name: {code:CommonProfileDir}\.clamwin\quarantine; Components: ClamAV; Permissions: authusers-full; Check: IsAllUsers
;for non-admin user create tese folders in user's profile dir, don't modify security
Name: {code:CommonProfileDir}\.clamwin\db; Components: ClamAV; Check: not IsAllUsers
Name: {code:CommonProfileDir}\.clamwin\log; Components: ClamAV; Check: not IsAllUsers
Name: {code:CommonProfileDir}\.clamwin\quarantine; Components: ClamAV; Check: not IsAllUsers
Name: {app}\bin; Components: ExplorerShell ClamWin
Name: {app}\lib; Components: ClamWin
Name: {app}\bin\img; Components: ClamWin
Name: {app}\locale; Components: ClamWin
[_ISTool]

[Components]
Name: ClamAV; Description: {cm:ClamAVFiles1}; Flags: fixed; Types: full custom
Name: ClamWin; Description: {cm:ClamWinFiles1}; Flags: fixed; Types: full custom
Name: ExplorerShell; Description: {cm:IntegrationExplorer1}; Types: full custom
Name: OutlookAddin; Description: {cm:IntegrationOutlook1}; Types: full; Check: IsOutlookInstalled
Name: LanguageSupport; Description: {cm:LanguageSupport1}; Types: full
Name: LanguageSupport\Arabic; Description: Arabic; Types: full
Name: LanguageSupport\Brasilian; Description: Brazilian; Types: full
Name: LanguageSupport\Dutch; Description: Nederlands (Dutch); Types: full
Name: LanguageSupport\French; Description: Français (French); Types: full
Name: LanguageSupport\German; Description: Deutsch (German); Types: full
Name: LanguageSupport\Hungarian; Description: Magyar (Hungarian); Types: full
Name: LanguageSupport\Italian; Description: Italiano (Italian); Types: full
Name: LanguageSupport\Polish; Description: Polski (Polish); Types: full
Name: LanguageSupport\Russian; Description: (Russian); Types: full
Name: LanguageSupport\Spanish; Description: Español (Spanish); Types: full
Name: LanguageSupport\Ukrainian; Description: Ukrainian; Types: full

[INI]
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: clamscan; String: {app}\bin\clamscan.exe; Check: IsIniValueEmpty(ExpandConstant('ClamAV*clamscan*{app}\bin\ClamWin.conf'))
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: freshclam; String: {app}\bin\freshclam.exe; Check: IsIniValueEmpty(ExpandConstant('ClamAV*freshclam*{app}\bin\ClamWin.conf'))
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: database; String: {code:CommonProfileDir}\.clamwin\db; Check: IsIniValueEmpty(ExpandConstant('ClamAV*database*{app}\bin\ClamWin.conf'))
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: quarantinedir; String: {code:CommonProfileDir}\.clamwin\quarantine; Check: IsIniValueEmpty(ExpandConstant('ClamAV*quarantinedir*{app}\bin\ClamWin.conf'))
Filename: {app}\bin\ClamWin.conf; Section: ClamAV; Key: logfile; String: {code:CommonProfileDir}\.clamwin\log\ClamScanLog.txt; Check: IsIniValueEmpty(ExpandConstant('ClamAV*logfile*{app}\bin\ClamWin.conf'))
Filename: {app}\bin\ClamWin.conf; Section: Updates; Key: dbupdatelogfile; String: {code:CommonProfileDir}\.clamwin\log\ClamUpdateLog.txt; Check: IsIniValueEmpty(ExpandConstant('Updates*dbupdatelogfile*{app}\bin\ClamWin.conf'))
Filename: {app}\bin\ClamWin.conf; Section: Updates; Key: time; String: {code:CurTime}; Check: IsIniValueEmpty(ExpandConstant('Updates*time*{app}\bin\ClamWin.conf'))

;[_ISToolDownload]
;Name: Sources_ClamWin; Description: {cm:ClamWinSourceCode1}; GroupDescription: Source Code; Flags: unchecked; Source: http://osdn.dl.sourceforge.net/sourceforge/clamwin/clamwin-0.88.2.3-src.zip; DestDir: {%TEMP|{localappdata}}; DestName: clamwin-src.zip; Components: Sources
;Name: Sources_ClamAV; Description: {cm:ClamAVSourceCode1}; GroupDescription: Source Code; Flags: unchecked; Source: http://osdn.dl.sourceforge.net/sourceforge/clamav/clamav-0.88.2.tar.gz; DestDir: {%TEMP|{localappdata}}; DestName: clamav-src.tar.gz; Components: Sources
;Name: Sources_Cygwin; Description: Cygwin Source Code; GroupDescription: Source Codes; Flags: unchecked; Source: http://mirrors.kernel.org/sources.redhat.com/cygwin/release/cygwin/cygwin-1.5.18-1-src.tar.bz2; DestDir: {%TEMP|{localappdata}}; DestName: cygwin-src.tar.bz2; Components: Sources

[Types]
Name: full; Description: {cm:FullInstallation1}
Name: custom; Description: {cm:CustomInstallation1}; Flags: iscustom

[UninstallDelete]
Name: {tmp}\ClamWin_Scheduler_Info; Type: files; Components: ClamWin
Name: {tmp}\ClamWin_Upadte_Time; Type: files; Components: ClamWin
Name: {app}; Type: filesandordirs
Name: {userappdata}\.clamwin; Type: filesandordirs
Name: {code:CommonProfileDir}\.clamwin; Type: filesandordirs

[Registry]
; write to HKLM and HKCR when user is admin
; and to HKCU when not

; ClamWin entries
Root: HKLM; Subkey: Software\Microsoft\Windows\CurrentVersion\Run; ValueType: string; ValueName: ClamWin; ValueData: """{app}\bin\ClamTray.exe"" --logon"; Flags: uninsdeletevalue; Components: ClamWin; Check: IsAllUsers
Root: HKLM; Subkey: Software\ClamWin; ValueType: string; ValueName: Path; ValueData: {app}\bin; Flags: uninsdeletekey deletevalue; Components: ClamWin; Check: IsAllUsers
Root: HKLM64; Subkey: Software\ClamWin; ValueType: string; ValueName: Path; ValueData: {app}\bin; Flags: uninsdeletekey deletevalue; Components: ClamWin; Check: IsAllUsers and IsWin64
Root: HKLM; Subkey: Software\ClamWin; ValueType: dword; ValueName: Version; ValueData: 9120; Flags: uninsdeletekey deletevalue; Components: ClamWin; Check: IsAllUsers
Root: HKCU; Subkey: Software\Microsoft\Windows\CurrentVersion\Run; ValueType: string; ValueName: ClamWin; ValueData: """{app}\bin\ClamTray.exe"" --logon"; Flags: uninsdeletevalue; Components: ClamWin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\ClamWin; ValueType: string; ValueName: Path; ValueData: {app}\bin; Flags: uninsdeletekey deletevalue; Components: ClamWin; Check: not IsAllUsers
Root: HKCU64; Subkey: Software\ClamWin; ValueType: string; ValueName: Path; ValueData: {app}\bin; Flags: uninsdeletekey deletevalue; Components: ClamWin; Check: not IsAllUsers and IsWin64
Root: HKCU; Subkey: Software\ClamWin; ValueType: dword; ValueName: Version; ValueData: 9120; Flags: uninsdeletekey deletevalue; Components: ClamWin; Check: not IsAllUsers

; ExplorerShell entries
Root: HKCR; Subkey: CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: {app}\bin\ExpShell.dll; Flags: uninsdeletekey; Components: ExplorerShell; Check: IsAllUsers
Root: HKCR64; Subkey: CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: {app}\bin\ExpShell64.dll; Flags: uninsdeletekey; Components: ExplorerShell; Check: IsAllUsers and IsWin64
Root: HKCR; Subkey: CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: Apartment; Flags: uninsdeletekey; ValueName: ThreadingModel; Components: ExplorerShell; Check: IsAllUsers
Root: HKCR64; Subkey: CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: Apartment; Flags: uninsdeletekey; ValueName: ThreadingModel; Components: ExplorerShell; Check: IsAllUsers and IsWin64
Root: HKCR; Subkey: *\shellex\ContextMenuHandlers\ClamWin; ValueType: string; ValueData: {{65713842-C410-4f44-8383-BFE01A398C90}; Flags: uninsdeletekey; Components: ExplorerShell; Check: IsAllUsers
Root: HKCR; Subkey: Folder\shellex\ContextMenuHandlers\ClamWin; ValueType: string; ValueData: {{65713842-C410-4f44-8383-BFE01A398C90}; Flags: uninsdeletekey; Components: ExplorerShell; Check: IsAllUsers

Root: HKCU; Subkey: Software\Classes\CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: {app}\bin\ExpShell.dll; Flags: uninsdeletekey; Components: ExplorerShell; Check: not IsAllUsers
Root: HKCU64; Subkey: Software\Classes\CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: {app}\bin\ExpShell64.dll; Flags: uninsdeletekey; Components: ExplorerShell; Check: not IsAllUsers and IsWin64
Root: HKCU; Subkey: Software\Classes\CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: Apartment; Flags: uninsdeletekey; ValueName: ThreadingModel; Components: ExplorerShell; Check: not IsAllUsers
Root: HKCU64; Subkey: Software\Classes\CLSID\{{65713842-C410-4f44-8383-BFE01A398C90}\InProcServer32; ValueType: string; ValueData: Apartment; Flags: uninsdeletekey; ValueName: ThreadingModel; Components: ExplorerShell; Check: not IsAllUsers and IsWin64
Root: HKCU; Subkey: Software\Classes\*\shellex\ContextMenuHandlers\ClamWin; ValueType: string; ValueData: {{65713842-C410-4f44-8383-BFE01A398C90}; Flags: uninsdeletekey; Components: ExplorerShell; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\Folder\shellex\ContextMenuHandlers\ClamWin; ValueType: string; ValueData: {{65713842-C410-4f44-8383-BFE01A398C90}; Flags: uninsdeletekey; Components: ExplorerShell; Check: not IsAllUsers

; OutlookAddin entries for all users
;delete COM InprocServer32 key if present (we moved to EXE COM server)
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\InprocServer32; Components: OutlookAddin; Check: IsAllUsers; Flags: deletekey noerror dontcreatekey
;install new EXE COM server
Root: HKCR; Subkey: AppID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\LocalServer32; ValueType: string; ValueData: {app}\bin\OlAddin.exe /Automate; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\PythonCOM; ValueType: string; ValueData: OlAddin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\PythonCOMPolicy; ValueType: string; ValueData: win32com.server.policy.EventHandlerPolicy; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\PythonCOMPath; ValueType: string; ValueData: {app}\bin; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\Implemented Categories\{{B3EF80D0-68E2-11D0-A689-00C04FD658FF}; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\Debugging; ValueType: string; ValueData: 0; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\ProgID; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: ClamWin.OutlookAddin; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKCR; Subkey: ClamWin.OutlookAddin\CLSID; ValueType: string; ValueData: {{E77FA584-1433-4af3-800D-AEC49BCCCB11}; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers

Root: HKLM; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: dword; ValueName: CommandLineSafe; ValueData: 0; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKLM; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: dword; ValueName: LoadBehavior; ValueData: 3; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKLM; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: string; ValueName: Description; ValueData: ClamWin Antivirus; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers
Root: HKLM; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: string; ValueName: FriendlyName; ValueData: ClamWin Antivirus; Flags: uninsdeletekey; Components: OutlookAddin; Check: IsAllUsers

; OutlookAddin entries for current user
;delete COM InprocServer32 key if present (we moved to EXE COM server)
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\InprocServer32; Components: OutlookAddin; Check: not IsAllUsers; Flags: deletekey dontcreatekey noerror
;install new COM EXE server
Root: HKCU; Subkey: Software\Classes\AppID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\LocalServer32; ValueType: string; ValueData: {app}\bin\OlAddin.exe /Automate; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\PythonCOM; ValueType: string; ValueData: OlAddin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\PythonCOMPolicy; ValueType: string; ValueData: win32com.server.policy.EventHandlerPolicy; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\PythonCOMPath; ValueType: string; ValueData: {app}\bin; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\Implemented Categories\{{B3EF80D0-68E2-11D0-A689-00C04FD658FF}; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\Debugging; ValueType: string; ValueData: 0; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\CLSID\{{E77FA584-1433-4af3-800D-AEC49BCCCB11}\ProgID; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\ClamWin.OutlookAddin; ValueType: string; ValueData: ClamWin.OutlookAddin; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Classes\ClamWin.OutlookAddin\CLSID; ValueType: string; ValueData: {{E77FA584-1433-4af3-800D-AEC49BCCCB11}; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers

Root: HKCU; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: dword; ValueName: CommandLineSafe; ValueData: 0; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: dword; ValueName: LoadBehavior; ValueData: 3; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: string; ValueName: Description; ValueData: ClamWin Antivirus; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers
Root: HKCU; Subkey: Software\Microsoft\Office\Outlook\Addins\ClamWin.OutlookAddin; ValueType: string; ValueName: FriendlyName; ValueData: ClamWin Antivirus; Flags: uninsdeletekey; Components: OutlookAddin; Check: not IsAllUsers

[Tasks]
Name: DownloadDB; Description: {cm:DownloadDB1}; GroupDescription: {cm:DownloadDB3}; Components: ClamAV
Name: desktopicon; Description: {cm:CreateDesktop1}; GroupDescription: {cm:CreateDesktop2}; Flags: unchecked
[UninstallRun]
Filename: {app}\bin\WClose.exe; WorkingDir: {app}\bin

[CustomMessages]
; Translated strings are in separate files in the locale directory,
; where they can be more easily managed.
#include "..\locale\ar_AE.iss"
#include "..\locale\cs_CZ.iss"
#include "..\locale\en_US.iss"
#include "..\locale\de_DE.iss"
#include "..\locale\es_ES.iss"
#include "..\locale\fr_FR.iss"
#include "..\locale\hu_HU.iss"
#include "..\locale\it_IT.iss"
#include "..\locale\nl_BE.iss"
#include "..\locale\pl_PL.iss"
#include "..\locale\ru_RU.iss"
#include "..\locale\uk_UA.iss"

[Code]
var
  AllUsers: Boolean;
  SetupCompleted: Boolean;
  Time: String;
  PreviousVersion, ThisVersion: Integer;
  AllUsersPage: TInputOptionWizardPage;

procedure InitializeWizard;
begin
  // Create the pages
  AllUsersPage := CreateInputOptionPage(wpLicense,
    ExpandConstant('{cm:InitializeWizard1}'), ExpandConstant('{cm:InitializeWizard2}'),
    ExpandConstant('{cm:InitializeWizard3}'),
    True, False);
  AllUsersPage.Add(ExpandConstant('{cm:InitializeWizard4}'));
  AllUsersPage.Add(ExpandConstant('{cm:InitializeWizard5}') + GetUserNameString() + ')');


  // Set default values, using settings that were stored last time if possible
  case GetPreviousData('AllUsersMode', '') of
    'anyone': AllUsers := True;
    'onlyme': AllUsers := False;
  end;
  if (AllUsers) then
    AllUsersPage.SelectedValueIndex := 0
  else
    AllUsersPage.SelectedValueIndex := 1;
end;

procedure RegisterPreviousData(PreviousDataKey: Integer);
var
  AllUsersModeStr: String;
begin
  // Store the settings so we can restore them next time
  case AllUsersPage.SelectedValueIndex of
    0: AllUsersModeStr := 'anyone';
    1: AllUsersModeStr := 'onlyme';
  end;
  SetPreviousData(PreviousDataKey, 'AllUsersMode', AllUsersModeStr);
end;

function RemoveSetup(keyname: String): Boolean;
var
	uninstall: String;
	resultcode: Integer;
begin
	if not RegKeyExists(HKEY_LOCAL_MACHINE, keyname) then begin
		Result := True;
		exit;
	end;
	if not RegQueryStringValue(HKEY_LOCAL_MACHINE, keyname, 'UninstallString',  uninstall) then begin
		Result := True;
		exit;
	end;
	if SuppressibleMsgBox(ExpandConstant('{cm:RemoveSetup1}'),
	              mbConfirmation, MB_YESNO, IDYES) = idYes then begin
		Result := Exec(RemoveQuotes(uninstall), ' /SILENT', '', SW_SHOWNORMAL, ewWaitUntilTerminated, resultcode);
	end else begin
		Result := False;
	end;
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  // Skip pages that shouldn't be shown
  // in win 98/me we don't ask for all users nor we do if an admin is not logged on
  if (PageID = AllUsersPage.ID) then begin
    if (not UsingWinNT()) then
	  Result := True
	else if (not IsAdminLoggedOn()) then begin
	  Result := True;
	end;
  end else
    Result := False;
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
	// Result := istool_download(CurPage);
	if CurPage = wpFinished then
		SetupCompleted := True
	else if CurPage = AllUsersPage.ID then begin
		case AllUsersPage.SelectedValueIndex of
			0: AllUsers := True;
			1: AllUsers := False;
		end;
		if ((not IsAdminLoggedOn()) and (not AllUsers)) then
			RemoveSetup('SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\ClamWin Antivirus_is1');
	end;
	Result := True
end;


function IsOutlookInstalled() : Boolean;
begin
    Result := RegKeyExists( HKCU, 'Software\Microsoft\Office\Outlook') or
			RegKeyExists( HKLM, 'Software\Microsoft\Office\Outlook') or
			RegKeyExists( HKLM, 'Software\Microsoft\Office\9.0\Outlook') or
			RegKeyExists( HKLM, 'Software\Microsoft\Office\10.0\Outlook') or
			RegKeyExists( HKLM, 'Software\Microsoft\Office\11.0\Outlook');
end;



function CheckNoAppMutex( mutexName: String; closeMsg: String) : Boolean;
begin
    Result := true;
    if Pos('/SUPPRESSMSGBOXES', Uppercase(GetCmdTail())) = 0 then begin
		while Result do begin
			if not CheckForMutexes(mutexName) then
				break;
			Result := MsgBox(closeMsg, mbConfirmation, MB_RETRYCANCEL) = idRetry;
		end;
	end;
end;

function NoOutlookRunning(): Boolean;
var
  closeit: String;
begin
    // Check if Outlook is running.
    closeit:= ExpandConstant('{cm:NoOutlookRunning1}') + #13 + #13 +
              ExpandConstant('{cm:NoOutlookRunning2}') + #13 +
              ExpandConstant('{cm:NoOutlookRunning3}')+ #13 + #13 +
              ExpandConstant('{cm:NoOutlookRunning4}') + #13 +
              ExpandConstant('{cm:NoOutlookRunning5}')
    Result := CheckNoAppMutex('_outlook_mutex_', closeit);
    // Check if MAPISP32.EXE is running - if it is, it implies something is screwey
    // with Outlook.
    if Result then begin
      closeit := ExpandConstant('{cm:NoOutlookRunning6}') + #13 + #13 +
                 ExpandConstant('{cm:NoOutlookRunning7}') + #13 + #13 +
                 ExpandConstant('{cm:NoOutlookRunning8}')
      Result := CheckNoAppMutex('InternetMailTransport', closeit);
    end;
end;

function IsWin9x(): Boolean;
begin
	Result := not UsingWinNT();
end;

function IsXPOrLater(): Boolean;
begin
	Result:= (GetWindowsVersion() >= $05010000);
end;



function IsAllUsers(): Boolean;
begin
	if not UsingWinNT() then
		Result := True
	else
		Result := AllUsers;
end;


procedure CloseClamWin();
var
	path, keyname: String;
	resultcode: Integer;
begin
	keyname := 'SOFTWARE\ClamWin';
	path := ''
	if not RegQueryStringValue(HKEY_CURRENT_USER, keyname, 'path',  path) then begin
		RegQueryStringValue(HKEY_LOCAL_MACHINE, keyname, 'path',  path)
	end;
	if path = '' then begin
		exit;
	end;
	if not CheckForMutexes('ClamWinTrayMutex01') then
        exit;
	if SuppressibleMsgBox(ExpandConstant('{cm:CloseClamWin1}') + #13 +
				 ExpandConstant('{cm:CloseClamWin2}'),
	              mbConfirmation, MB_YESNO, IDYES) = idYes then begin
	    path := RemoveQuotes(path)+'\WClose.exe';
		Exec(path, '', '', SW_SHOWNORMAL, ewWaitUntilTerminated, resultcode);
	end;
end;


function InitializeSetup(): Boolean;
var
	value: Cardinal;
begin
	if (not UsingWinNT()) then
		AllUsers := True
	else
		AllUsers := IsAdminLoggedOn();

	ThisVersion := 9120;
	value := 0;
	if not RegQueryDWordValue(HKEY_CURRENT_USER, 'SOFTWARE\Clamwin', 'Version',  value) then begin
		value := 0;
		RegQueryDWordValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Clamwin', 'Version',  value)
	end;
	PreviousVersion := value;
	if PreviousVersion > ThisVersion then begin
		SuppressibleMsgBox(ExpandConstant('{cm:InitializeSetup1}') + #13 +
				 ExpandConstant('{cm:InitializeSetup2}'), mbError, MB_OK, IDOK);
		Result := False;
		exit;
	end;


	SetupCompleted := False;
	Time := GetDateTimeString('hh:nn', #0, ':') + ':00';
	Result := NoOutlookRunning();
	if Result then CloseClamWin();
end;

procedure DeInitializeSetup();
var
	filename: String;
begin
	DeleteFile(ExpandConstant('{%TEMP|{localappdata}}\clamwin-src.zip'));
	DeleteFile(ExpandConstant('{%TEMP|{localappdata}}\clamav-src.tar.gz'));
	//DeleteFile(ExpandConstant('{%TEMP|{localappdata}}\cygwin-src.tar.bz2'));
	if (SetupCompleted) and (PreviousVersion < 35) then begin
		filename := ExpandConstant('{app}\bin\Tray.exe');
		if (FileExists(filename)) then begin
			DeleteFile(filename);
		end;
		filename := ExpandConstant('{app}\bin\OutlookAddin.exe');
		if (FileExists(filename)) then begin
			DeleteFile(filename);
		end;
		filename := ExpandConstant('{app}\bin\ExplorerShell.dll');
		if (FileExists(filename)) then begin
			RestartReplace(filename, '');
			SuppressibleMsgBox(ExpandConstant('{cm:DeInitializeSetup1}'),
				mbInformation, MB_OK, IDOK);
		end;
	end;
end;


function
BaseDir(Default: String): String;
begin
	if IsAllUsers() then begin
		Result := ExpandConstant('{pf}');
	end else begin
		Result := ExpandConstant('{userappdata}');
	end;
end;

function CurTime(Default: String): String;
begin
	Result := Time;
end;

function TempDir(Default: String): String;
begin
	Result := GetTempDir();
end;

function CommonProfileDir(Default: String): String;
var
	AppData, Tmp: String;
	i, position: Integer;
begin
	if IsAllUsers() then begin
		AppData := RemoveBackslash(ExpandConstant('{commonappdata}'));
	end else begin
		AppData := ExpandConstant('{userappdata}');
	end;
	// remove last directory from the path (Ususally Application Data)
	Tmp := AppData;
	While True do begin
		i := Pos('\', Tmp);
		if i <> 0 then begin
			Tmp := Copy(Tmp, i + 1, Length(Tmp) - i);
		end else begin
			Break;
		end;
	end;
	position := Length(AppData) - Length(Tmp);
	if position > 0 then begin
		Result := RemoveBackslash(Copy(AppData, 0, position));
	end else begin
		Result := AppData;
	end;
end;


function DesktopDir(Default: String): String;
begin
	if IsAllUsers() then begin
		Result := ExpandConstant('{commondesktop}');
	end else begin
		Result := ExpandConstant('{userdesktop}');
	end;
end;



function IsIniValueEmpty(Param : String): Boolean;
var
	Section, Key, Filename, Val, TempStr, Default : String;
	EndPos: Integer;
begin
	Default := '';
	TempStr := Param;

	EndPos := Pos('*', TempStr);
	Section := Copy(TempStr, 0, EndPos - 1);
	TempStr := Copy(TempStr, EndPos + 1, Length(TempStr) - EndPos)

	EndPos := Pos('*', TempStr);
	Key := Copy(TempStr, 0, EndPos - 1);
	TempStr := Copy(TempStr, EndPos + 1, Length(TempStr) - EndPos)

	Filename := TempStr;

	Val:= Trim(GetIniString(Section, Key, Default, Filename));
	Result := (Val = '')

end;

function IsTemplateConfig(Filename: String): Boolean;
begin
	Result := (not IsWin9x()) and (not FileExists(Filename));
end;

function NoMainInc(): Boolean;
var
	Temp: String;
begin
	Result := not DirExists(CommonProfileDir(Temp) + '\.clamwin\db\main.inc')
end;

function NoDailyInc(): Boolean;
var
	Temp: String;
begin
	Result := not DirExists(CommonProfileDir(Temp) + '\.clamwin\db\daily.inc')
end;
